apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

group = "com.yandex.android"
ext.artifactId = "authsdk"

apply from: '../publishing.gradle'

def getBuildNumber() {
    (System.getenv('BUILD_NUMBER') ?: Integer.MAX_VALUE) as int
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode getBuildNumber()
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [YANDEX_OAUTH_HOST: "oauth.yandex.ru"]

        def addConstant = { constantName, constantValue ->
            manifestPlaceholders += [(constantName): constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        addConstant("ACTION_YA_SDK_LOGIN", "com.yandex.auth.action.YA_SDK_LOGIN")
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testVariants.all {
        it.mergedFlavor.manifestPlaceholders += [
                YANDEX_CLIENT_ID: "fcdddf83a97843ae80815c1c9247015b",
        ]
    }

    unitTestVariants.all {
        it.mergedFlavor.manifestPlaceholders += [
                YANDEX_CLIENT_ID: "fcdddf83a97843ae80815c1c9247015b",
        ]
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.activity:activity-ktx:1.7.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.browser:browser:1.5.0'
    testImplementation 'com.android.support:support-annotations:28.0.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.mockito:mockito-core:4.1.0'
    testImplementation 'io.mockk:mockk:1.12.4'
}
